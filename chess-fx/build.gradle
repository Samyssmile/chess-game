plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.1.0'
}

group 'de.chess'
version '1.0.0'
evaluationDependsOn(':chess-lib')
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

javafx {
    version = "21.0.8"
    modules = ['javafx.controls', 'javafx.media' ]
}
repositories {
    mavenCentral()
}
application {
    mainClass = 'de.chess.fx.app.ChessFX'
}

dependencies {
    implementation project(':chess-lib')

    testImplementation 'org.junit.jupiter:junit-jupiter:5.13.4'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}



test {
    useJUnitPlatform()
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes(
                'Main-Class': 'de.chess.fx.app.GameLauncher'
        )
    }

    // Stelle sicher, dass chess-lib zuerst gebaut wird:
    dependsOn project(':chess-lib').tasks.named('jar')

    // Nimm *explizit* das Artefakt von :chess-lib als Input:
    from(project(':chess-lib').tasks.named('jar').map { zipTree(it.archiveFile) })

    // UND (wenn du wirklich ein "Fat-Jar" willst) alle externen Jars:
    from({
        configurations.runtimeClasspath
                .filter { it.name != "${project(':chess-lib').name}-${project(':chess-lib').version}.jar" }
                .collect { it.isDirectory() ? it : zipTree(it) }
    })
}
